GAL22V10

; Address Management Unit (AMU)

; Active-low signals '/' qualified
; Pins [1, 12]
phi2  a8        a9  a10 a11 a12   a13   a14     a15     pb0     pb1     GND
; Pins [13, 24]
/pb7  /csPokey  /io  NC  NC  /ram  /ref  /csGtia /csPia  a14Rom  /csRom  VCC

; Equations
; <signal_active> = <expression>
; <expression>: List of AND product terms concatenated with the OR operator (disjunctive normal form).

; rom_os_hi        =  a15 &  a14 &  a13               & /ref &  pb0
;                  #  a15 &  a14 & /a13 &  a12 &  a11 & /ref &  pb0
; io               =  a15 &  a14 & /a13 &  a12 & /a11 & /ref
; rom_os_lo        =  a15 &  a14 & /a13 & /a12        & /ref &  pb0
; rom_basic        =  a15 & /a14 &  a13               & /ref & /pb1
; rom_os_self_test = /a15 &  a14 & /a13 &  a12 & /a11 & /ref &  pb0 & /pb7
; ram              = /csRom & /io                     & /ref

; IO
io       =  a15 &  a14 & /a13 &  a12 & /a11 & /ref
csPia    =  io & /a10 &  a9 &  a8
csPokey  =  io & /a10 &  a9 & /a8
csGtia   =  io & /a10 & /a9 & /a8

; ROM    =  rom_os_hi # rom_os_lo # rom_basic # rom_os_self_test
csRom    =  a15 &  a14 &  a13               & /ref &  pb0 #  a15 &  a14 & /a13 &  a12 &  a11 & /ref &  pb0 #  a15 &  a14 & /a13 & /a12        & /ref &  pb0 #  a15 & /a14 &  a13               & /ref & /pb1  #  /a15 &  a14 & /a13 &  a12 & /a11 & /ref &  pb0 & /pb7

; a14Rom =  rom_basic
a14Rom   =  a15 & /a14 &  a13               & /ref & /pb1

; RAM    =  ram
ram      = /csRom & /io                     & /ref

DESCRIPTION

Address Space

[D8, FF] ram|rom_os_hi
[D0, D7] io |
[C0, CF] ram|rom_os_lo
[A0, BF] ram|rom_basic
[80, 9F] ram|
[58, 7F] ram|
[50, 57] ram|rom_os_self_test
[40, 4F] ram|
[00, 3F] ram|

32 KiB ROM

[60-7F] unused (8 KiB)
[40-5F] rom_basic (8 KiB)
[18-3F] rom_os_hi (10 KiB)
[10-17] rom_os_self_test (2 KiB)
[00-0F] rom_os_lo (4 KiB)
